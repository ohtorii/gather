/**/



#g_dll_ohtorii_tools=val(getarg(30));
$label=getarg(0);
$arg1=getarg(1);
$arg2=getarg(2);
$arg3=getarg(3);
$arg4=getarg(4);
$arg5=getarg(5);

$g_root_macro_directory =dllfuncstrw(#g_dll_ohtorii_tools,"StaticStatusGetRootMacroDirectory");


call $label, $arg1, $arg2, $arg3, $arg4, $arg5 ;
endmacro $$return;


do_action:
	$$action=$$1;
	call $$action;
	return $$return;

/*
実装	機能
		function! s:kind.action_table.cd.func(candidate) abort "{{{
		function! s:kind.action_table.lcd.func(candidate) abort "{{{
		function! s:kind.action_table.project_cd.func(candidate) abort "{{{
		function! s:kind.action_table.tabnew_cd.func(candidate) abort "{{{
		function! s:kind.action_table.tabnew_lcd.func(candidate) abort "{{{
		function! s:kind.action_table.narrow.func(candidate) abort "{{{
		function! s:kind.action_table.vimshell.func(candidate) abort "{{{			→ コマンドプロンプト
		function! s:kind.action_table.tabvimshell.func(candidate) abort "{{{
△		function! s:kind.action_table.vimfiler.func(candidate) abort "{{{			-> 秀丸ファイラー
		function! s:kind.action_table.tabvimfiler.func(candidate) abort "{{{

*/

command_prompt:
	##first_item_index	= 0;
	call GetPathName ##first_item_index;
	$$text=$$return;
	execmacro $g_root_macro_directory+"\\internal\\util_get_parent_folder_name.mac",$$text;
	$$parent_folder=getresultex(-1);
	runex "cmd"
			, 0 			//sync	  0:async 1:sync
			, 0, "" 		//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
			, 0, "" 		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
			, 0, "" 		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
			, 2, $$parent_folder 		//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
			, 1 			//show	  0:auto 1:show 2:hide
			, 1 			//nodraw  0:draw 1:no draw
			, 0 			//unicode 0:ansi 2:unicode
			;
	return "1";


explorer:
	##first_item_index	= 0;
	call GetPathName ##first_item_index;
	$$text=$$return;
	execmacro $g_root_macro_directory+"\\internal\\util_get_parent_folder_name.mac",$$text;
	$$parent_folder=getresultex(-1);
	runex "explorer " + "\""+$$parent_folder+"\""
			, 0 			//sync	  0:async 1:sync
			, 0, "" 		//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
			, 0, "" 		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
			, 0, "" 		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
			, 0,"" 		//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
			, 1 			//show	  0:auto 1:show 2:hide
			, 1 			//nodraw  0:draw 1:no draw
			, 0 			//unicode 0:ansi 2:unicode
			;
	return "1";


hidemaru_filer:
	call find_hidemaru_filer;
	$$path = $$return;
	if($$path==""){
		return "0";
	}
	##first_item_index	= 0;
	call GetPathName ##first_item_index;
	$$text=$$return;
	execmacro $g_root_macro_directory+"\\internal\\util_get_parent_folder_name.mac",$$text;
	$$parent_folder=getresultex(-1);

	runex $$path+" " +"\""+$$parent_folder+"\""
			, 0 			//sync	  0:async 1:sync
			, 0, "" 		//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
			, 0, "" 		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
			, 0, "" 		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
			, 0, "" 		//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
			, 1 			//show	  0:auto 1:show 2:hide
			, 1 			//nodraw  0:draw 1:no draw
			, 0 			//unicode 0:ansi 2:unicode
			;
	return "1";


file:
	##first_item_index	= 0;
	$$text=dllfuncstrw(#g_dll_ohtorii_tools,"GetSelectionActionFileName",##first_item_index);
	execmacro $g_root_macro_directory+"\\internal\\util_get_parent_folder_name.mac",$$text;
	$$parent_folder=getresultex(-1)+"\\";
	$$source="file";
	##ret=dllfuncw(#g_dll_ohtorii_tools,"StartCandidate",$$source,$$parent_folder);
	if(##ret){
		return "1";
	}
	return "0";


narrow:
	##first_item_index	= 0;
	$$folder=dllfuncstrw(#g_dll_ohtorii_tools,"GetSelectionActionDirectoryName",##first_item_index);
	$$source="file";
	##ret=dllfuncw(#g_dll_ohtorii_tools,"StartCandidate",$$source,$$folder);
	if(##ret){
		return "1";
	}
	return "0";


//////////////////////////////////////////////////////////////////////////////
//	ユーティリティ
//////////////////////////////////////////////////////////////////////////////
find_hidemaru_filer:
	$$x64="C:\\Program Files\\HmFilerClassic\\HmFilerClassic.exe";
	$$x86="C:\\Program Files (x86)\\HmFilerClassic\\HmFilerClassic.exe";
	if(existfile($$x64)){
		return $$x64;
	}
	if(existfile($$x86)){
		return $$x86;
	}

	return "";


GetPathName:
	##index=##1;
	$$text=dllfuncstrw(#g_dll_ohtorii_tools,"GetSelectionActionFileName",##index);
	if($$text!=""){
		return $$text;
	}
	return dllfuncstrw(#g_dll_ohtorii_tools,"GetSelectionActionDirectoryName",##index);
