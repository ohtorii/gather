/*ハイライト
*/

/*
＊取り得る値
	""			初期値
	"restore"	設定を元に戻す
*/
$g_static_variable="unity.kind.hilight";

#g_dll_ohtorii_tools=val(getarg(30));
$label				=getarg(0);
$arg				=getarg(1);

call $label, $arg;
endmacro $$return;



////////////////////////////////////////////////////////////////////////////
//	Request.
////////////////////////////////////////////////////////////////////////////
do_action:
	$$action=$$1;
	call $$action;
	return $$return;


//ハイライトのプレビュー
preview:
	$$value=getstaticvariable($g_static_variable , 0 );
	if($$value==""){
		//マクロ終了後に適用したハイライト設定を戻すためにコールバックを登録する。
		$$args=currentconfigset+"\t"+str(configstate)+"\t"+str(hidemaruhandle(0));
		##ret = dllfuncw(#g_dll_ohtorii_tools,"SetCallBack_ProcessFinish",currentmacrofilename,"restore_configset",$$args);
		//コールバックを二重登録しないようにフラグを設定しておく。
		setstaticvariable $g_static_variable, "restore",0;
	}
	$$text=dllfuncstrw(#g_dll_ohtorii_tools,"GetSelectionText",0);
	##temporary=0; //(memo) 0 == 一時的な設定
	##flag =	0x00000001 | /*強調表示 */
				0x00000002 | /*複数行の強調 */
				0x00000004 | /*ツリー定義 */
				0x00000008 | /*カラー */
				0x00000010 ; /*#ifdef等の詳細 */
	loadhilight $$text, ##temporary, ##flag;
	if(! tabmode){
		//ウインドウモードの場合。
		return "1";
	}

	##search_hidemaru_handle = dllfuncw(#g_dll_ohtorii_tools,"StaticStatusGetSearchHidemaruHandle");
	##target_hidemaru_handle = dllfuncw(#g_dll_ohtorii_tools,"StaticStatusGetTargetHidemaruHandle");
	if(tabgroup[##search_hidemaru_handle] != tabgroup[##target_hidemaru_handle]){
		//異なるタブグループ
		return ;
	}

	//ハイライト設定を目視でしっかり確認するために、
	call GetSleepTime;
	##milli_sec=##return;
	sleep ##milli_sec;
	return "1";


GetSleepTime:
	$$root_macro_directory =dllfuncstrw(#g_dll_ohtorii_tools,"StaticStatusGetRootMacroDirectory");
	$$ini_filename=$$root_macro_directory+"\\config.ini";
	##milli_sec=getininumw($$ini_filename,"action", "sleep");
	if(##milli_sec < 0){
		##milli_sec=0;
	}
	return ##milli_sec;


//ハイライトを適用する
execute:
	$$text=dllfuncstrw(#g_dll_ohtorii_tools,"GetSelectionText",0);
	##permanent=1;	//(memo) 1 == 設定を保存する
	##flag =	0x00000001 | /*強調表示 */
				0x00000002 | /*複数行の強調 */
				0x00000004 | /*ツリー定義 */
				0x00000008 | /*カラー */
				0x00000010 ; /*#ifdef等の詳細 */
	loadhilight $$text, ##permanent, ##flag;
	//設定を元に戻すコールバック(restore_configset:)を無視する
	setstaticvariable $g_static_variable, "",0;
	return "1";


restore_configset:
	$$value=getstaticvariable($g_static_variable , 0 );
	if($$value!="restore"){
		return "1";
	}
	setstaticvariable $g_static_variable, "",0;

	##num = split($$args, $$1, "\t" );
	if(##num!=3){
		return "0";
	}

	$$config			=$$args[0];
	##state 			=val($$args[1]);
	##target_hidemaru	=val($$args[2]);

	##current_hidemaru=hidemaruhandle(0);
	setactivehidemaru ##target_hidemaru;
	if(##state & 0x0001){
		//元から一時的な設定
		configset $$config, 0;
	}else{
		configset $$config, 1;
	}

	##ret=##return;
	setactivehidemaru ##current_hidemaru;
	return str(##ret);

