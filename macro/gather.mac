/* 作成中

usage:
  gather.mac mode

mode:
  file_mru 			履歴
  directory_mru		履歴
  file_rec			カレントディレクトリ以下
   window 			ウインドウ一覧
    :
    :

Unite-vimの説明。
https://www.youtube.com/watch?v=Co4Np4SXOOc
*/

//空白文字の定義
$g_space="\r\n\t 　";


#g_open_newfile = false;
#g_new_hidemaru = 0;
#g_old_hidemaru = hidemaruhandle(0);

//DLL
#g_hmjre_dll 	= 0;
#g_dll_ohtorii_tools=0;


/*****************************************************************************
	メイン処理
*****************************************************************************/
$g_old_searchbuffer = searchbuffer;
#g_old_searchoption = searchoption;
#g_old_hidemaru     = hidemaruhandle(0);
call main;
call CloseFileIfOpen;
setsearch $g_old_searchbuffer, #g_old_searchoption;
endmacro;

main:
	call NewfileIfExist;
	if(! ##return){
		return false;
	}
	call LoadDll;
	if(! ##return){
		return false;
	}
	$$mode=getarg(0);

	//ファイル一覧を非表示の秀丸へ出力する
	//	ファイル履歴、ブックマーク、ウインドウ一覧、
	//一時ファイルに保存する
	$$temp_filename=currentmacrodirectory+"\\temp_history.txt";

	//絞り込み検索
	$$key_buffer="";
	$$cursor_pos="0";
	$$prev_pattern="999";
	##prev_tickcount=0;
	while(true){
		execmacro currentmacrodirectory+"\\..\\..\\command-line\\command-line-internal\\input-keys.mac", $$key_buffer, $$cursor_pos;
		$$keybord_buffer=getresultex(-1);

		execmacro currentmacrodirectory+"\\..\\..\\command-line\\command-line-internal\\parse_result.mac" , "keycode"		, $$keybord_buffer;
		$$keycode=getresultex(-1);
		execmacro currentmacrodirectory+"\\..\\..\\command-line\\command-line-internal\\parse_result.mac" , "cursor"		, $$keybord_buffer;
		$$cursor_pos=getresultex(-1);
		execmacro currentmacrodirectory+"\\..\\..\\command-line\\command-line-internal\\parse_result.mac" , "keybuffer"	, $$keybord_buffer;
		$$key_buffer=getresultex(-1);

		if($$keycode=="13"){
			/*Enterキーが押された。
			選択ファイルに対してディフォルトアクションを実行する
			*/
			$$key_buffer="";
		}else if($$keycode=="27"){	break;} 	//Escapeが押された
		else if($$keycode=="16"){	up;}		//ctrl-p
		else if($$keycode=="14"){	down;}		//ctrl-n
		else if($$keycode=="18"){	call Mark;}		//ctrl-space
		//}else if (##in == 0x1D){		call _Up;		// [↑]
		//}else if (##in == 0x1F){		call _Down; 	//[↓]
		else{
			//一致するパターンをファイルから行検索する
			if(($$prev_pattern != $$key_buffer) && (300<(tickcount-##prev_tickcount))){
				call Filter $$temp_filename,$$key_buffer;
				$$prev_pattern=$$key_buffer;
				##prev_tickcount=tickcount;
			}
		}

		/*
		if(タブキーが押された){
			アクションを選択する
		}

		*/
	}
	
	call FreeDll;
	return true;

Mark:
	//カーソル行に色を塗る
	disabledraw;
	$$marker=getcolormarker(0x0001,"gather");
	debuginfo 1;
	debuginfo "$$marker="+$$marker;

	if($$marker=="000000FF"){
		//マーク削除
		selectline 0;
		deletecolormarker "gather";
		escape;
	}else{
		//マークする
		selectline 0;
		colormarker 0x000000ff,-1,-1,0,0x00,"gather";
		escape;
	}
	enabledraw;
	return;

Filter:
	$$file_list=$$1;
	call TrimString $$2;
	$$pattern=$$return;
	
	disabledraw;
	selectall;
	backspace;
	$$str = dllfuncstrw(#g_dll_ohtorii_tools,"Filter",$$pattern);
	insert($$str);
	call HilightWorld $$pattern;
	enabledraw;
	return result;

HilightWorld:
	$$search_words=$$1;
	call SearchWordsToHidemaruRegex $$search_words;
	$$regex=$$return;
	setsearch $$regex,0x00000800|0x00000010;
	hilightfound 1;
	return;

SearchWordsToHidemaruRegex:
	/*空白区切りの文字列を秀丸エディタの正規表現に変換する。
	[in]	"hoge 123 .txt"
	[out]	"hoge|123|\.txt"
	*/
	$$word_list=$$1;
	$$escaped=quote($$word_list);
	$$regex = dllfuncstr(#g_hmjre_dll,"ReplaceRegular", "[" + $g_space + "]+", $$escaped, 0, "|", 2);
	return $$regex;

TrimString:
	while(1){
	  ##word=strlen($$1);
	  //文字列の先頭に空白文字がある場合、空白文字を削除
	  if(strstr($g_space,leftstr($$1,2))!=-1)$$1=rightstr($$1,##word-2);
	  else if(strstr($g_space,rightstr($$1,2))!=-1)$$1=leftstr($$1,##word-2);
	  //ここまで全角文字の対処
	  else if(strstr($g_space,leftstr($$1,1))!=-1)$$1=rightstr($$1,##word-1);
	  else if(strstr($g_space,rightstr($$1,1))!=-1)$$1=leftstr($$1,##word-1);
	  else break;
	}
	return $$1;

NewfileIfExist:
	#g_open_newfile = false;
	if(filetype=="new"){
		return true;
	}
	//編集中のテキストを上書きしないための対応。
	//新規ファイルを作成しそこへコマンドの実行結果を出力する。
	newfile;
	if(result==0){
		return false;
	}
	#g_open_newfile=true;
	#g_new_hidemaru=hidemaruhandle(0);
	return true;

CloseFileIfOpen:
	if(#g_open_newfile){
		setactivehidemaru	#g_old_hidemaru;
		closehidemaruforced #g_new_hidemaru;

		#g_old_hidemaru=0;
		#g_new_hidemaru=0;
	}

LoadDll:
	#g_dll_ohtorii_tools = loaddll(currentmacrodirectory + "\\internal\\ohtorii_tools_x64.dll");
	if (#g_dll_ohtorii_tools==0) {
		message "internal\\ohtorii_tools.dllのロードに失敗しました";
		return false;
	}
	#g_hmjre_dll=loaddll("HmJre.dll");
	if(#g_hmjre_dll==0){
		message "HmJre.dll のロードに失敗しました\n" +
				+ "HmJre.dllが秀丸エディタのディレクトリに存在するか確認してください";
		return false;
	}
	
	return true;

FreeDll:
	freedll #g_dll_ohtorii_tools;
	#g_dll_ohtorii_tools=0;
	
	freedll #g_hmjre_dll;
	#g_hmjre_dll=0;
	return true;
	