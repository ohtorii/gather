/* common.

(引数)
	リクエスト	...

（説明）
	第二引数以降はリクエストの種類にり異なります。


（リクエストの種類）
	- get_property
	- do_action
	//- get_default_action	後で実装

（リクエスト毎の引数）
	- get_property
		[0] get_property

	- do_action
		[0] 		do_action
		[1] 		アクション名
		[2 ... 9]	予約
		[10]		ファイル数
		[10+1]		ファイル名 1
		[10+2]		ファイル名 2
						:
						:
		[10+N] 		ファイル名 N

	//* get_default_action
	//	[1]	なし
*/

$request=getarg(0);
call $request;
endmacro $$return;



////////////////////////////////////////////////////////////////////////////
//	Request.
////////////////////////////////////////////////////////////////////////////
get_property:
	return R"([property]
name=common
description=common actions.
default_action=nop
'継承するアクション（スペースで区切る）
'inheritance=

'アクション
[action.nop]
'表示に使用する名前
name=nop
'秀丸マクロのラベル名
function=nop
'アクションの説明
description=Do nothing.
'アクション実行後に終了するかどうか
is_quit=false
'アクションを複数選択に対して実行するかどうか
is_multi_selectable=true

[action.yank]
name=yank
function=yank
description=Yank the candidate "word" or "action__text".
is_multi_selectable=true

[action.yank_escape]
name=yank_escape
function=yank_escape
description=Yank the escaped candidate "word" or "action__text".

[action.insert]
name=insert
function=insert
description=Insert the candidate word or text before the cursor.

[action.append]
name=append
function=append
description=Insert the candidate word or text after the cursor.

[action.insert_directory]
name=insert_directory	
function=insert_directory	
description=Insert the candidate directory before the cursor.

[action.append_directory]
name=append_directory	
function=append_directory	
description=Insert the candidate directory after the cursor.

[action.preview]
name=preview
function=preview
description=Preview the candidate text.
is_quit=false
is_multi_selectable=false

[action.echo]
name=echo
function=echo
description=Echo candidates for debug.
)";

/*対応する動作無し
ex	Input the escaped candidate text into command line.
*/



do_action:
	/*call SplitFilelist, getarg(2);
	if(! ##return){
		return false;
	}*/
	call MakeFileList;
	$$action = getarg(1);
	call $$action;
	return ##return;



////////////////////////////////////////////////////////////////////////////
//	Action.
////////////////////////////////////////////////////////////////////////////
nop:
	return ;

append:
	##i=0;
	while(##i<#g_filelist_num){
		if(1<=##i){
			insertreturn;
		}
		insert($g_filelist[##i]);
		##i = ##i + 1;
	}
	return ;

echo:
	//debug　用途なので後回し
	return ;

ex:
	return ;

insert:
	##i=0;
	while(##i<#g_filelist_num){
		if(1<=##i){
			insertreturn;
			up;
		}
		insertfix($g_filelist[##i]);
		##i = ##i + 1;
	}
	return ;

append_directory:
	call FileListToDirectoryList;
	if(! ##return){
		return ;
	}
	call append;
	return ;

insert_directory:
	call FileListToDirectoryList;
	if(! ##return){
		return ;
	}
	call insert;
	return ;

preview:
	$$text="";
	{
		##old_hidemaru=hidemaruhandle(0);
		openfile "/h /n " + $g_filelist[0];
		if(!result){
			return;
		}
		##new_hidemaru=hidemaruhandle(0);

		selectall;
		$$text=gettext2(seltopcolumn,seltoplineno,selendcolumn,selendlineno);

		setactivehidemaru	##old_hidemaru;
		closehidemaruforced ##new_hidemaru;
	}

	##output_dll=loaddll("HmOutputPane.dll");
	if(!result){
		return;
	}

	{
		//アウトプット枠クリア
		##h=dllfunc(##output_dll,"GetWindowHandle",hidemaruhandle(0));
		##ret=sendmessage(##h,0x111/*WM_COMMAND*/,1009,0);//1009=クリア
		//ファイル内容を出力する
		##ret=dllfunc(##output_dll,"Output",hidemaruhandle(0),$$text);
		if(false){
			//WM_KEYDOWN
			##ret=sendmessage(##h, 0x0100, 0x11, 0x011D0001);
			##ret=sendmessage(##h, 0x0100, 0x24, 0x01470001);
			//WM_KEYUP
			##ret=sendmessage(##h, 0x0101, 0x24, 0xc1470001);
			##ret=sendmessage(##h, 0x0101, 0x11, 0xc11d0001);
		}

	}

	freedll ##output_dll;
	return ;

yank:
	call MakeClipboardString;
	setclipboard $$return;
	return ;

yank_escape:
	call MakeClipboardString;
	setclipboard quote($$return);
	return ;




////////////////////////////////////////////////////////////////////////////
//	Utility.
////////////////////////////////////////////////////////////////////////////
MakeFileList:
	#g_filelist_num=val(getarg(10));
	##i=0;
	while(##i<#g_filelist_num){
		$g_filelist[##i]=getarg(11+##i);
		##i = ##i + 1;
	}
	return ;


MakeClipboardString:
	$$str="";
	##i=0;
	while(##i<#g_filelist_num){
		if(1<=##i){
			$$str = $$str + "\x0D\x0A";
		}
		$$str = $$str + $g_filelist[##i];
		##i = ##i + 1;
	}
	return $$str;


LoadDll:
	##dll=loaddll(macrodir+"\\"+$$1);
	if(##dll!=0){
		return ##dll;
	}
	##dll=loaddll(hidemarudir+"\\"+$$1);
	return ##dll;


SplitFilelist:
	/* ファイルリストをグローバル変数に格納します。

	（格納先のグローバル変数）
	$g_filelist;		//ファイル名
	#g_filelist_num;	//ファイル名の個数
	*/

	call LoadDll "DengakuDLL.dll";
	if(##return==0){
		return false;
	}
	##dll=##return;

	#g_filelist_num=0;
	$$token = dllfuncstr(##dll,"GETTOKEN",$$1,"\t");
	while (1) {
		if($$token != ""){
			$g_filelist[#g_filelist_num] = $$token;
			#g_filelist_num = #g_filelist_num + 1;
		}
		if (dllfunc(##dll,"HASMORETOKENS") == 0) {
			break;
		}
		$$token = dllfuncstr(##dll,"GETTOKEN","","\t");
	}
	freedll ##dll;

	if(#g_filelist_num==0){
		return false;
	}
	return true;


FileListToDirectoryList:
	/* ファイルリストをディレクトリイストへ変更する
	*/
	call LoadDll "ht_tools.dll";
	if(##return==0){
		return false;
	}
	##dll=##return;
	##i=0;
	while(##i<#g_filelist_num){
		$$dir = dllfuncstr(##dll, "GetParentFolder", $g_filelist[##i]);
		$g_filelist[##i]=$$dir;
		##i = ##i + 1;
	}
	freedll ##dll;
	return true;
