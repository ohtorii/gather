/* タブ一覧
*/

/////////////////////////////////////////////////////////////////////////////
//メイン処理
/////////////////////////////////////////////////////////////////////////////

//検索を実行している秀丸エディタのハンドル
#g_search_hidemaru_handle=0;

#g_dll_ohtorii_tools=val(getarg(30));
$label=getarg(0);
$arg=getarg(1);
call $label, $arg;
endmacro $$return;


gather_candidates:
	/*
	【タブモードの場合】
		タブグループ１		<- 選択させない
			foo.txt			<- 選択対象
			bar.txt			<- 選択対象
		タブグループ２		<- 選択させない
			hoge.txt		<- 選択対象

	【ウインドウモードの場合】
			foo.txt			<- 選択対象
			bar.txt			<- 選択対象
			hoge.txt		<- 選択対象
	*/
	#g_search_hidemaru_handle=dllfuncw(#g_dll_ohtorii_tools,"StaticStatusGetSearchHidemaruHandle");
	if(tabmode){
		call gater_tab_mode;
	}else{
		call gater_window_mode;
	}
	return $$return;


gater_tab_mode:
	//タブモードの場合
	$$prefix ="\t";
	$$postfix="";
	##i = 0;
	while( ##i < tabgrouptotal ) {
		##success = dllfuncw(#g_dll_ohtorii_tools, "AppendCandidateHeader", sprintf("タブグループ%d", ##i), "");
		if(!##success){
			return "0";
		}

		##grouptop = gettabhandle( true, ##i, 0 );
		##j = 0;
		while( ##j < tabtotal[##grouptop] ) {
			##hidemaru_handle = gettabhandle( true, ##i, ##j );

			//(memo)検索を実行している秀丸エディタは候補から外す。
			if(##hidemaru_handle != #g_search_hidemaru_handle){
				call make_text_tab_mode ##hidemaru_handle;
				$$text=$$return;

				call make_descrption, ##hidemaru_handle;
				$$description=$$return;

				##candidate_index	= dllfuncw(#g_dll_ohtorii_tools, "AppendCandidateFix", $$prefix, $$text, $$postfix, $$description);
				if(##candidate_index==-1){
					return "0";
				}
				##ret = dllfuncw(#g_dll_ohtorii_tools,"SetCandidateActionWindowHandle",##hidemaru_handle);
				if(! ##ret){
					return "0";
				}
			}

			##j = ##j + 1;
		}
		##i = ##i + 1;
	}
	return "1";


gater_window_mode:
		##i=0;
	while(##i < hidemarucount){
		##handle=hidemaruhandle(##i);

		//(memo)検索を実行している秀丸エディタは候補から外す。
		if(##handle != #g_search_hidemaru_handle){
			call make_text_window_mode ##handle;
			$$text=$$return;

			call make_descrption, ##handle;
			$$description=$$return;

			##candidate_index = dllfuncw(#g_dll_ohtorii_tools, "AppendCandidate", $$text, $$description);
			if(##candidate_index==-1){
				return "0";
			}
			##ret = dllfuncw(#g_dll_ohtorii_tools,"SetCandidateActionWindowHandle",##handle);
			if(! ##ret){
				return "0";
			}
		}

		##i = ##i + 1;
	}
	return "1";


make_text_tab_mode:
	##handle=##1;
	return gettitle(3,##handle);

make_text_window_mode:
	##handle=##1;
	return gettitle(0,##handle);


make_descrption:
	if(true){
		return "";
	}else{
		##handle=##1;
		$$description=basename2[##handle];
		if(#g_show_handle){
			$$description=$$description+"\t"+str(##handle);
		}else{
			//pass
		}
		return $$description;
	}
