/* タブ一覧
*/

/////////////////////////////////////////////////////////////////////////////
//カスタマイズ
/////////////////////////////////////////////////////////////////////////////




/////////////////////////////////////////////////////////////////////////////
//メイン処理
/////////////////////////////////////////////////////////////////////////////
#g_dll_ohtorii_tools=val(getarg(30));
$label=getarg(0);
$arg=getarg(1);
call $label, $arg;
endmacro $$return;


gather_candidates:
	/*
	【タブモードの場合】
		タブグループ１		<- 選択させない
			foo.txt			<- 選択対象
			bar.txt			<- 選択対象
		タブグループ２		<- 選択させない
			hoge.txt		<- 選択対象

	【ウインドウモードの場合】
			foo.txt			<- 選択対象
			bar.txt			<- 選択対象
			hoge.txt		<- 選択対象
	*/
	call gater_tab_mode;
	return $$return;


gater_tab_mode:
	//タブモードの場合
	##i = 0;
	while( ##i < tabgrouptotal ) {
		##success = dllfuncw(#g_dll_ohtorii_tools, "AppendCandidateHeader", sprintf("タブグループ%d", ##i), "");
		if(!##success){
			return "0";
		}

		##grouptop = gettabhandle( true, ##i, 0 );
		##j = 0;
		while( ##j < tabtotal[##grouptop] ) {
			##hidemaru_handle = gettabhandle( true, ##i, ##j );
			call make_text ##hidemaru_handle;
			$$text=$$return;

			##candidate_index	= dllfuncw(#g_dll_ohtorii_tools, "AppendCandidateFix", "\t", $$text, "", "");
			if(##candidate_index==-1){
				return "0";
			}
			##ret = dllfuncw(#g_dll_ohtorii_tools,"SetCandidateActionWindowHandle",##hidemaru_handle);
			if(! ##ret){
				return "0";
			}

			##j = ##j + 1;
		}
		##i = ##i + 1;
	}
	return "1";




make_text:
	##handle=##1;
	$$name = filename2[##handle];
	if($$name==""){
		$$name=filetype[##handle];
	}
	if(updated[##handle]){
		$$name=$$name+"  *";
	}
	return $$name;
