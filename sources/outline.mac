/* Outline
*/



/////////////////////////////////////////////////////////////////////////////
//グローバル変数
/////////////////////////////////////////////////////////////////////////////
#g_outline_count=0;
#g_outline_line[0]=0;
$g_outline_text[0]="";
#g_outline_indent[0]=0;

$g_target_hidemaru_filename="";



/////////////////////////////////////////////////////////////////////////////
//メイン処理
/////////////////////////////////////////////////////////////////////////////
#g_dll_ohtorii_tools=val(getarg(30));
$label=getarg(0);
$arg=getarg(1);
call $label, $arg;
endmacro $$return;


gather_candidates:
	//
	//候補をグローバル変数へ格納する
	//
	{
		##old_hidemaru		=hidemaruhandle(0);
		##target_hidemaru	=dllfuncw(#g_dll_ohtorii_tools,"StaticStatusGetTargetHidemaruHandle");

		setactivehidemaru ##target_hidemaru;
		disabledraw;
		##xOrg=x;
		##yOrg=y;

		call gather_candidates_main;
		##ret=##return;

		moveto ##xOrg,##yOrg;
		enabledraw;
		setactivehidemaru ##old_hidemaru;
	}
	if(##ret==false){
		return "0";
	}

	//
	//候補をDLL側へ追加する
	//
	##success = dllfuncw(#g_dll_ohtorii_tools, "AppendCandidateHeader", "アウトライン", "行番号");
	if(!##success){
		return "0";
	}
	{
		##i=0;
		while(##i<#g_outline_count){
			call indent_to_prefix #g_outline_indent[##i];
			$$prefix=$$return;

			##candidate_index=dllfuncw(#g_dll_ohtorii_tools, "AppendCandidateFix", $$prefix, $g_outline_text[##i], "", str(#g_outline_line[##i]));
			if(##candidate_index==-1){
				return "0";
			}
			##success=dllfuncw(#g_dll_ohtorii_tools, "SetCandidateActionFileName", $g_target_hidemaru_filename);
			##success=dllfuncw(#g_dll_ohtorii_tools, "SetCandidateActionLine", #g_outline_line[##i]);

			##i = ##i + 1;
		}
	}
	return str(##ret);


indent_to_prefix:
/*
*/
	$$prefix="";
	##indent=##1;
	##i=0;
	while(##i<##indent){
		$$prefix=$$prefix+"    ";
		##i = ##i + 1;
	}
	return $$prefix;


gather_candidates_main:
	#g_outline_count=0;
	##n=outlineitemcount;
	while(1){
		if(##n <= #g_outline_count){
            break;
        }
		$g_outline_text[#g_outline_count]  =getoutlineitem(#g_outline_count,0);
		#g_outline_line[#g_outline_count]  =val(getoutlineitem(#g_outline_count,1));
		#g_outline_indent[#g_outline_count]=val(getoutlineitem(#g_outline_count,2));

		#g_outline_count = #g_outline_count + 1;
	}
	$g_target_hidemaru_filename=filename2;
	return true;
