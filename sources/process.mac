/*プロセス
*/

$g_source_name="process";
#g_dll_ohtorii_tools=val(getarg(0));
$label=getarg(1);
$arg=getarg(2);
call $label, $arg;
endmacro $$return;

get_property:
	//ソースのプロパティをini形式で返す
	return R"([property]
name=process
description=起動しているプロセスの一覧
default_kind=common
candidate_type=function
)";

gather_candidates:
	/*
	タブグループ１[0x0000111]	<- 選択対象
		foo.txt
		bar.txt
	タブグループ２[0x0000112]	<- 選択対象
		hoge.txt

	0x....はウインドウハンドル

	*/

	##candidate_index	= dllfuncw(#g_dll_ohtorii_tools, "CandidatesAppendHeader", $g_source_name, "イメージ名", "PID\tメモリ使用量");
	call create_process_candidates;
	return $$return;


create_process_candidates:
	/*プロセスの候補を作成する。

	＊以下VBSを秀丸マクロへ移植
	Set process = CreateObject("WbemScripting.SWbemLocator").ConnectServer.ExecQuery("Select * From Win32_Process")
	For Each item In process
	  WScript.Echo item.Name & "," & item.ProcessId & "," & item.WorkingSetSize/1024
	  'item.Description, item.ProcessId "," & item.ExecutablePath
	Next
	*/
	##obj=createobject("WbemScripting.SWbemLocator");
	if(! getresultex(10)){
		debuginfo "Failed:createobject";
		return "0";
	}
	##ConnectServer=member(##obj,"ConnectServer");
	if(! getresultex(10)){
		debuginfo "Failed:ConnectServer";
		return "0";
	}
	##Win32_Process=callmethod_returnobj(##ConnectServer,"ExecQuery","Select * From Win32_Process");
	if(! getresultex(10)){
		debuginfo "Failed:ExecQuery";
		return "0";
	}
	while(1){
		##item=getcollection(##Win32_Process);
		if(##item==0){
			break;
		}
		$$name=getpropstr( ##item, "Name" );
		/*if(! getresultex(10)){
			debuginfo "Failed:Name";
			return "0";
		}*/
		$$process_id=str(getpropnum( ##item, "ProcessId" ));//uint32

		call number_format val(getpropstr( ##item, "WorkingSetSize" ))/1024;
		$$working_set_size=$$return + "K";

		//$$command_line=getpropstr(##item,"CommandLine");
		##candidate_index = dllfuncw(#g_dll_ohtorii_tools, "CandidatesAppend", $g_source_name, $$name, $$process_id+"\t"+$$working_set_size);
	}
	##item=getcollection(##Win32_Process,3);
	releaseobject(##obj);
	return "1";

number_format:
	/*３桁ごとのカンマ区切り文字列に変換する
	1234567 -> "1,234,567"
	*/
	##sep=3;

	##number = ##1;
	$$n=str(##number);

	##len=strlen($$n);
	//debuginfo "##len="+str(##len);
	##remain=##len%##sep;//余り
	//debuginfo "##remain="+str(##remain);

	//(memo) $$result="1"
	$$result = leftstr($$n,##remain);

	/*(memo)
	[##i=0]	$$result="1,234"
	[##i=1]	$$result="1,234,567"
	*/
	##loop=##len/##sep;
	##i=0;
	while(##i<##loop){
		$$part = midstr($$n,##remain+(##i*3),##sep);
		if($$result==""){
			$$result = $$result + $$part;
		}else{
			$$result = $$result + "," + $$part;
		}

		##i = ##i + 1;
	}
	return $$result;