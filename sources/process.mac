/*プロセス
*/

#g_dll_ohtorii_tools=val(getarg(30));
$label=getarg(0);
$arg1=getarg(1);
$arg2=getarg(2);
$arg3=getarg(3);
$arg4=getarg(4);
$arg5=getarg(5);
call $label, $arg1, $arg2, $arg3, $arg4, $arg5;
endmacro $$return;


gather_candidates:
	##success	= dllfuncw(#g_dll_ohtorii_tools, "AppendCandidateHeader", "イメージ名", "PID\tメモリ使用量");
	if(!##success){
		return "0";
	}
	call create_process_candidates;
	return $$return;


create_process_candidates:
	/*プロセスの候補を作成する。

	＊以下VBSを秀丸マクロへ移植
	Set process = CreateObject("WbemScripting.SWbemLocator").ConnectServer.ExecQuery("Select * From Win32_Process")
	For Each item In process
	  WScript.Echo item.Name & "," & item.ProcessId & "," & item.WorkingSetSize/1024
	  'item.Description, item.ProcessId "," & item.ExecutablePath
	Next
	*/
	##obj=createobject("WbemScripting.SWbemLocator");
	if(! getresultex(10)){
		debuginfo "Failed:createobject";
		return "0";
	}
	##ConnectServer=member(##obj,"ConnectServer");
	if(! getresultex(10)){
		debuginfo "Failed:ConnectServer";
		return "0";
	}
	##Win32_Process=callmethod_returnobj(##ConnectServer,"ExecQuery","Select * From Win32_Process");
	if(! getresultex(10)){
		debuginfo "Failed:ExecQuery";
		return "0";
	}
	while(1){
		##item=getcollection(##Win32_Process);
		if(##item==0){
			break;
		}
		$$name=getpropstr( ##item, "Name" );
		##process_id=getpropnum( ##item, "ProcessId" );//uint32
		$$process_id=str(##process_id);

		call number_format val(getpropstr( ##item, "WorkingSetSize" ))/1024;
		$$working_set_size=$$return + "K";

		##success = dllfuncw(#g_dll_ohtorii_tools, "AppendCandidate", $$name, $$process_id+"\t"+$$working_set_size);
		if(! ##success){
			return "0";
		}
		##success = dllfuncw(#g_dll_ohtorii_tools, "SetCandidateActionProcessId", ##process_id);
		if(! ##success){
			return "0";
		}
	}
	##item=getcollection(##Win32_Process,3);
	releaseobject(##obj);
	return "1";

number_format:
	/*３桁ごとのカンマ区切り文字列に変換する
	1234567 -> "1,234,567"
	*/
	##sep=3;

	##number = ##1;
	$$n=str(##number);

	##len=strlen($$n);
	##remain=##len%##sep;//余り

	//(memo) $$result="1"
	$$result = leftstr($$n,##remain);

	/*(memo)
	[##i=0]	$$result="1,234"
	[##i=1]	$$result="1,234,567"
	*/
	##loop=##len/##sep;
	##i=0;
	while(##i<##loop){
		$$part = midstr($$n,##remain+(##i*3),##sep);
		if($$result==""){
			$$result = $$result + $$part;
		}else{
			$$result = $$result + "," + $$part;
		}

		##i = ##i + 1;
	}
	return $$result;