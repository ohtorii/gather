/* ウインドウ一覧
*/
#g_dll_ohtorii_tools=val(getarg(0));
$label=getarg(1);
$arg=getarg(2);
call $label, $arg;
endmacro $$return;

get_property:
	//ソースのプロパティをini形式で返す
	return R"([property]
name=window
description=candidates from tab list
default_kind=window
candidate_type=function
)";

gather_candidates:
	/*
	タブグループ１[0x0000111]	<- 選択対象
		foo.txt
		bar.txt
	タブグループ２[0x0000112]	<- 選択対象
		hoge.txt

	0x....はウインドウハンドル

	*/
	$$source_name="window";
	$$user_dataname = "hidemaru_handle";

	##i = 0;
	while( ##i < tabgrouptotal ) {
		##grouptop			= gettabhandle( true, ##i, 0 );
		##hidemaru_handle	= ##grouptop;
		##candidate_index	= dllfuncw(#g_dll_ohtorii_tools, "CandidatesAppend", $$source_name, sprintf("タブグループ%d[%d]",tabgroup[##grouptop],##hidemaru_handle), "");
		if(##candidate_index == -1){
			return "0";
		}

		##ret = dllfuncw(#g_dll_ohtorii_tools,"CandidatesSetUserDataNumeric",##candidate_index,$$user_dataname,##hidemaru_handle);
		if(! ##ret){
			return "0";
		}

		##j = 0;
		while( ##j < tabtotal[##grouptop] ) {
			##handle = gettabhandle( true, ##i, ##j );
			$$name = basename[##handle];
			if($$name==""){
				$$name=filetype[##handle];
			}
			if(updated[##handle]){
				$$name=$$name+"  *";
			}
			$$name=sprintf("%s [%d]",$$name,##handle);
			##candidate_child_index = dllfuncw(#g_dll_ohtorii_tools, "CandidatesAppendChild", ##candidate_index, $$name, "");
			if(##candidate_child_index==-1){
				return "0";
			}

		   	##j = ##j + 1;
		}
		##i = ##i + 1;
	}

	return "1";