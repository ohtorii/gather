/* ウインドウ一覧
*/

/////////////////////////////////////////////////////////////////////////////
//カスタマイズ
/////////////////////////////////////////////////////////////////////////////

////ハンドルの値を表示するかどうか
#g_show_handle=false;



/////////////////////////////////////////////////////////////////////////////
//メイン処理
/////////////////////////////////////////////////////////////////////////////
#g_dll_ohtorii_tools=val(getarg(30));
$label=getarg(0);
$arg=getarg(1);
call $label, $arg;
endmacro $$return;


gather_candidates:
	/*
	タブグループ１[0x0000111]	<- 選択対象
		foo.txt
		bar.txt
	タブグループ２[0x0000112]	<- 選択対象
		hoge.txt

	0x....はウインドウハンドル

	*/
	##i = 0;
	while( ##i < tabgrouptotal ) {
		##grouptop			= gettabhandle( true, ##i, 0 );
		##hidemaru_handle	= ##grouptop;
		if(#g_show_handle){
			$$handle=str(##hidemaru_handle);
		}else{
			$$handle="";
		}

		##candidate_index	= dllfuncw(#g_dll_ohtorii_tools, "AppendCandidate", sprintf("タブグループ%d",tabgroup[##grouptop]), $$handle);
		if(##candidate_index == -1){
			return "0";
		}

		##ret = dllfuncw(#g_dll_ohtorii_tools,"SetCandidateActionWindowHandle",##hidemaru_handle);
		if(! ##ret){
			return "0";
		}

		##j = 0;
		while( ##j < tabtotal[##grouptop] ) {
			##handle = gettabhandle( true, ##i, ##j );
			$$name = basename[##handle];
			if($$name==""){
				$$name=filetype[##handle];
			}
			if(updated[##handle]){
				$$name=$$name+"  *";
			}
			if(#g_show_handle){
				$$handle=str(##handle);
			}else{
				$$handle="";
			}
			##candidate_child_index = dllfuncw(#g_dll_ohtorii_tools, "AppendChildCandidate", $$name, $$handle);
			if(##candidate_child_index==-1){
				return "0";
			}

		   	##j = ##j + 1;
		}
		##i = ##i + 1;
	}

	return "1";