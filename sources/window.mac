/* ウインドウ一覧
*/

/////////////////////////////////////////////////////////////////////////////
//カスタマイズ
/////////////////////////////////////////////////////////////////////////////

//ハンドルの値を表示するかどうか
#g_show_handle=false;



/////////////////////////////////////////////////////////////////////////////
//メイン処理
/////////////////////////////////////////////////////////////////////////////
#g_dll_ohtorii_tools=val(getarg(30));
$label=getarg(0);
$arg=getarg(1);
call $label, $arg;
endmacro $$return;


gather_candidates:
	/*
	【タブモードの場合】
		タブグループ１	[0x00000111]	<- 選択対象
			foo.txt						<- 選択させない
			bar.txt						<- 選択させない
		タブグループ２	[0x00000112]	<- 選択対象
			hoge.txt					<- 選択させない




	【ウインドウモードの場合】
			foo.txt		[0x00000123]	<- 選択対象
			bar.txt		[0x00000456]	<- 選択対象
			hoge.txt	[0x00000789]	<- 選択対象



	※0x....はウインドウハンドル
	　グローバル変数（#g_show_handle）で表示／非表示を制御できます。


	*/
	if(tabmode){
		call gater_tab_mode;
	}else{
		call gater_window_mode;
	}
	return $$return;


gater_tab_mode:
	##i = 0;
	while( ##i < tabgrouptotal ) {
		##grouptop			= gettabhandle( true, ##i, 0 );
		##hidemaru_handle	= ##grouptop;
		if(#g_show_handle){
			$$handle=str(##hidemaru_handle);
		}else{
			$$handle="";
		}

		##candidate_index	= dllfuncw(#g_dll_ohtorii_tools, "AppendCandidate", sprintf("タブグループ%d",tabgroup[##grouptop]), $$handle);
		if(##candidate_index == -1){
			return "0";
		}

		##ret = dllfuncw(#g_dll_ohtorii_tools,"SetCandidateActionWindowHandle",##hidemaru_handle);
		if(! ##ret){
			return "0";
		}

		##j = 0;
		while( ##j < tabtotal[##grouptop] ) {
			##handle = gettabhandle( true, ##i, ##j );

			call make_text ##handle;
			$$text=$$return;

			call make_descrption, ##handle;
			$$description=$$return;

			##candidate_child_index = dllfuncw(#g_dll_ohtorii_tools, "AppendChildCandidate", $$text, $$description);
			if(##candidate_child_index==-1){
				return "0";
			}

		   	##j = ##j + 1;
		}
		##i = ##i + 1;
	}
	return "1";


gater_window_mode:
	##i=0;
	while(##i < hidemarucount){
		##handle=hidemaruhandle(##i);

		call make_text ##handle;
		$$text=$$return;

		call make_descrption, ##handle;
		$$description=$$return;

		##candidate_index = dllfuncw(#g_dll_ohtorii_tools, "AppendCandidate", $$text, $$description);
		if(##candidate_index==-1){
			return "0";
		}
		##ret = dllfuncw(#g_dll_ohtorii_tools,"SetCandidateActionWindowHandle",##handle);
		if(! ##ret){
			return "0";
		}

		##i = ##i + 1;
	}
	return "1";


make_text:
	##handle=##1;
	$$name = filename2[##handle];
	if($$name==""){
		$$name=filetype[##handle];
	}
	if(updated[##handle]){
		$$name=$$name+"  *";
	}
	return $$name;

make_descrption:
	##handle=##1;
	$$description=basename2[##handle];
	if(#g_show_handle){
		$$description=$$description+"\t"+str(##handle);
	}else{
		//pass
	}
	return $$description;
