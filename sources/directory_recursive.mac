/*カレントディレクトリ以下のファイルを開く
*/
$g_root_macro_directory 	=getstaticvariable("$unity.root_macro_directory", 1);
$g_current_working_directory=getstaticvariable("$unity.current_working_directory", 1);

#g_dll_ohtorii_tools=val(getarg(0));
$label=getarg(1);
$arg1=getarg(2);
$arg2=getarg(3);
$arg3=getarg(4);
$arg4=getarg(5);
$arg5=getarg(6);
call $label, $arg1, $arg2, $arg3, $arg4, $arg5;
endmacro $$return;

get_property:
	//ソースのプロパティをini形式で返す
	return R"([property]
name=directory_recursive
description=candidates from directory by recursive
default_kind=directory
candidate_type=async_file
)";


gather_candidates:
	$$root_dir=$$1;
	$$temp_filename="c:\\temp\\filelist.txt";
	if($$root_dir==""){
		//カレントディレクトリを使用する
		$$root_dir=".";
	}

	##ret=dllfuncw(#g_dll_ohtorii_tools,"FileRegistAfterDeleteFile",$$temp_filename);
	if(! ##ret){
		return "";
	}

	/* memo
	\で終端しているディレクトリ名をダブルクォーテーションで囲ってコマンドライン引数で渡すときの対策。
	*/
	call remove_terminate $$root_dir;
	$$root_dir=$$return;

	$$exe=$g_root_macro_directory+R"(\resources\file_searcher.exe)";
	$$batch = sprintf(R"(%s "%s" directory "%s" "%s" *)", $$exe, $$temp_filename, $g_current_working_directory, $$root_dir);

	$$cmd = "cmd /U /C "+$$batch;
	/*
	debuginfo "directory="+directory;
	debuginfo "$$batch="+$$batch;
	debuginfo "$$cmd="+$$cmd;
	*/
	runex $$cmd
			, 1 			//sync	  0:async 1:sync
			, 0, "" 		//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
			, 7, "" 		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
			, 1, "" 		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
			, 0, "" 		//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
			, 2 			//show	  0:auto 1:show 2:hide
			, 1 			//nodraw  0:draw 1:no draw
			, 2 			//unicode 0:ansi 2:unicode
			;
	if(! result){
		return "";
	}

	return $$temp_filename;


remove_terminate:
	/*ディレクトリ名終端の \/ を取り除く

	（変換の例）
	"c:temp\"	-> "c:\temp"
	"c:temp/"	-> "c:\temp"
	*/
	$$last=rightstr($$1,1);
	if(($$last=="\\") || ($$last=="/")){
		return leftstr($$1,strlen($$1)-1);
	}
	return $$1;
