/*ディレクトリ
*/
#g_dll_ohtorii_tools=val(getarg(30));
$label=getarg(0);
$arg1=getarg(1);
$arg2=getarg(2);
$arg3=getarg(3);
$arg4=getarg(4);
$arg5=getarg(5);

$g_root_macro_directory =dllfuncstrw(#g_dll_ohtorii_tools,"StaticStatusGetRootMacroDirectory");

call $label, $arg1, $arg2, $arg3, $arg4, $arg5;
endmacro $$return;


/*
\ 'ignore_globs' : [
      \         '.hg/**', '.git/**', '.bzr/**', '.svn/**', '.vs/**'
*/
gather_candidates:
	$$root_dir=$$1;
	call gather_candidates_main,$$root_dir;
	$$ret=$$return;
	return $$ret;


gather_candidates_main:
	$$root_dir=$$1;
	if($$root_dir==""){
		$$pattern="*.*";
	}else{
		$$pattern=$$root_dir+"\\*.*";
	}

	//カレントディレクトリを分かりやすくするために、候補ヘッダにカレントディレクトリを設定する。
	call GetAbsolutePathName $$root_dir;
	$$abs_root_dir=$$return;
	##r = dllfuncw(#g_dll_ohtorii_tools, "AppendCandidateHeader", "["+$$abs_root_dir+"]", "");
	if(!##r){
		return "0";
	}

	//////////////////////////////////////////////////////////////////////////
	//ディレクトリを登録する
	//////////////////////////////////////////////////////////////////////////
	$$dir="..\\";
	##r=dllfuncw(#g_dll_ohtorii_tools, "AppendCandidate", $$dir,"");
	##r=dllfuncw(#g_dll_ohtorii_tools, "SetCandidateActionDirectoryName", $$dir);

	##objFSO 		=createobject("Scripting.FileSystemObject");
	##objFolder		=member( ##objFSO, "GetFolder", $$abs_root_dir );
	##objCollection	=getpropobj(##objFolder,"subfolders");
	while(1){
		##objItem = getcollection( ##objCollection );
		if(##objItem==0){
			break;
		}
		$$dir=getpropstr(##objItem,"Name")+"\\";
		##r=dllfuncw(#g_dll_ohtorii_tools, "AppendCandidate", $$dir,"");
		##r=dllfuncw(#g_dll_ohtorii_tools, "SetCandidateActionDirectoryName", $$dir);
	}
	releaseobject ##objFSO;
	return "1";


GetAbsolutePathName:
	##fso=createobject("Scripting.FileSystemObject");
	$$abs_filename = member(##fso,"GetAbsolutePathName",$$1);
	releaseobject ##fso;
	return $$abs_filename;
