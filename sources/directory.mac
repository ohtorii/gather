/*ディレクトリ
*/
$g_root_macro_directory =getstaticvariable("$unity.root_macro_directory", 1);

#g_dll_dengaku=0;
#g_dll_ohtorii_tools=val(getarg(0));
$label=getarg(1);
$arg1=getarg(2);
$arg2=getarg(3);
$arg3=getarg(4);
$arg4=getarg(5);
$arg5=getarg(6);
call $label, $arg1, $arg2, $arg3, $arg4, $arg5;
endmacro $$return;

get_property:
	//ソースのプロパティをini形式で返す
	return R"([property]
name=directory
description=candidates from directory list
default_kind=directory
candidate_type=function
)";

/*
\ 'ignore_globs' : [
      \         '.hg/**', '.git/**', '.bzr/**', '.svn/**', '.vs/**'
*/
gather_candidates:
	$$root_dir=$$1;
	execmacro $g_root_macro_directory+"\\internal\\load_dll.mac", "DengakuDLL.dll";
	#g_dll_dengaku=val(getresultex(-1));
	if(#g_dll_dengaku==0){
		return "0";
	}

	call gather_candidates_main,$$root_dir;
	$$ret=$$return;

	freedll(#g_dll_dengaku);
	#g_dll_dengaku=0;

	return $$ret;


gather_candidates_main:
	$$root_dir=$$1;
	if($$root_dir==""){
		$$pattern="*.*";
	}else{
		$$pattern=$$root_dir+"\\*.*";
	}

	//////////////////////////////////////////////////////////////////////////
	//ディレクトリを登録する
	//////////////////////////////////////////////////////////////////////////
	##r = dllfunc(#g_dll_dengaku,"ENUMDIR",$$pattern);
	while (1) {
		$$dir = dllfuncstr(#g_dll_dengaku,"FINDNEXT");
		if ($$dir == "") {
			break;
		}
		if($$dir == "."){
			continue;
		}
		$$dir=$$dir+"\\";
		##r=dllfuncw(#g_dll_ohtorii_tools, "AppendCandidate", $$dir,"");
		##r=dllfuncw(#g_dll_ohtorii_tools, "SetCandidateActionPath", $$dir);
	}
	return "1";
