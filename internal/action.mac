/* アクションから候補を取得する

引数	ohtorii_tools_dll_handle, input_action_filename
返値	成功	"1"
		失敗	"0"
*/


//==========================================================================
//グローバル変数
//==========================================================================
#g_dll_ht_tool=0;
#g_dll_ohtorii_tools=val(getarg(0));

//------------------------
//iniファイルの情報
//------------------------
//アクションのプロパティ
$g_ini_property_name			="";
$g_ini_property_description		="";
$g_ini_property_default_action	="";
$g_ini_property_inheritance		="";

//アクション毎の情報
$g_ini_action_name[0]					="";
$g_ini_action_function[0]				="";
$g_ini_action_description[0]			="";
$g_ini_action_is_multi_selectable[0]	="";
$g_ini_action_is_quit[0]				="";
#g_ini_action_num						=0;



//==========================================================================
//メイン処理
//==========================================================================
call main getarg(1);
endmacro $$return;


main:
	$$action_filename=$$1;

	/* ソースからiniファイルを作りパースする。
	*/
	call CreateActionIniFile $$action_filename;
	$g_ini_temp_filename=getresultex(-1);
	if($g_ini_temp_filename==""){
		return false;
	}
	/*候補を作る
	*/
	call GatherCandidates $g_ini_temp_filename;
	$g_candidate_filename=$$return;
	
	//call DumpActionProperty;
	return;
	
	
	if($g_candidate_filename==""){
		return false;
	}

	##ret=dllfuncw(#g_dll_ohtorii_tools,"SetCandidateList",$g_candidate_filename, $g_ini_name, $g_ini_description);
	if(! ##ret){
		return false;
	}
	return true;


CreateActionIniFile:
	/*
	返値	iniファイル名(一時ファイル)
	*/
	$$action_filename=$$1;
	execmacro $$action_filename, "get_property";
	$$ini=getresultex(-1);
	if($$ini==""){
		return "";
	}
	execmacro currentmacrodirectory+"\\create_temp_file.mac", "ini", ".txt";
	$$temp_filename=getresultex(-1);
	if($$temp_filename==""){
		return "";
	}

	##ret=dllfuncw(#g_dll_ohtorii_tools,"WriteToFile",$$temp_filename,$$ini);
	if(! ##ret){
		deletefile $$temp_filename;
		return "";
	}
	return $$temp_filename;


//#region Gather
GatherCandidates:
	/*アクションの候補リストを作成する
	*/
	$$ini_filename=$$1;
	call ParseIni $$ini_filename;
	if(!##return){
		call ClearGlobalIniString;
		return "";
	}
	
	$$list="";
	##i=0;
	while(##i<#g_ini_action_num){
		$$line=$g_ini_action_name[##i]+"\t\t"+$g_ini_property_name+"\t\t"+$g_ini_action_description[##i];
		$$list=$$list+$$line+"\n";
		##i = ##i + 1;
	}
	call ClearGlobalIniString;
	
	execmacro currentmacrodirectory+"\\create_temp_file.mac", "action", ".txt";
	$$temp_filename=getresultex(-1);
	if($$temp_filename==""){
		return "";
	}
	##ret=dllfuncw(#g_dll_ohtorii_tools,"WriteToFile",$$temp_filename,$$list);
	if(! ##ret){
		deletefile $$temp_filename;
		return "";
	}
	return $$temp_filename;

	
ParseIni:
	$$ini_filename=$$1;
	//debuginfo "$$ini_filename="+$$ini_filename;
	$g_ini_property_name			=getinistrw($$ini_filename,"property","name");
	$g_ini_property_description		=getinistrw($$ini_filename,"property","description");
	$g_ini_property_default_action	=getinistrw($$ini_filename,"property","default_action");
	$g_ini_property_inheritance		=getinistrw($$ini_filename,"property","inheritance");

	call LoadDll;
	if(! ##return){
		return false;
	}

	/*アクションのセクション名を集める
	*/
	$$action_sections[0]="";
	##action_sections_num=0;

	##num = dllfunc(#g_dll_ht_tool,"ReadSectionNames", $$ini_filename);
	##i=0;
	while(##i < ##num){
		$$name = dllfuncstr(#g_dll_ht_tool,"GetSectionName", ##i);
		call CheckActionsSection $$name;
		if(##return){
			$$action_sections[##action_sections_num]=$$name;
			##action_sections_num = ##action_sections_num + 1;
		}
		##i = ##i + 1;
	}
	call FreeDll;

	if(false){
		//debug
		debuginfo 1;
		debuginfo "==== action sections. ====";
		##i=0;
		while(##i<##action_sections_num){
			debuginfo $$action_sections[##i];
			##i = ##i + 1;
		}
	}

	/*アクションのセクションから情報を読み取る
	*/
	##i=0;
	while(##i<##action_sections_num){
		$$section				=$$action_sections[##i];
		$$name					=getinistrw($$ini_filename,$$section,"name");
		$$function				=getinistrw($$ini_filename,$$section,"function");
		$$description			=getinistrw($$ini_filename,$$section,"description");
		$$is_multi_selectable	=getinistrw($$ini_filename,$$section,"is_multi_selectable");
		$$is_quit				=getinistrw($$ini_filename,$$section,"is_quit");
		
		$g_ini_action_name[##i]					=$$name;
		$g_ini_action_function[##i]				=$$function;
		$g_ini_action_description[##i]			=$$description;
		$g_ini_action_is_multi_selectable[##i]	=$$is_multi_selectable;
		$g_ini_action_is_quit[##i]				=$$is_quit;
		
		##i = ##i + 1;
	}
	#g_ini_action_num=##i;
	
	return true;


CheckActionsSection:
	/*アクションのセクション名かどうかを調べる
	*/
	$$section_name=$$1;
	if(leftstr($$section_name,7)=="action."){
		return true;
	}
	return false;


ClearGlobalIniString:
	/*iniファイルのパースで使用したグローバル変数の文字列をクリアする
	*/
	##i=0;
	while(##i<#g_ini_action_num){
		$g_ini_action_name[##i]					="";
		$g_ini_action_function[##i]				="";
		$g_ini_action_description[##i]			="";
		$g_ini_action_is_multi_selectable[##i]	="";
		$g_ini_action_is_quit[##i]				="";
		##i = ##i + 1;
	}
	return ;


//#region DLL
LoadDllFuzzy:
	##dll=loaddll(macrodir+"\\"+$$1);
	if(##dll!=0){
		return ##dll;
	}
	##dll=loaddll(hidemarudir+"\\"+$$1);
	if(##dll!=0){
		return ##dll;
	}
	message $$1+" のロードに失敗しました\n「秀丸エディタ・マクロ」のディレクトリに存在するか確認してください";
	return 0;


LoadDll:
	call LoadDllFuzzy "ht_tools.dll";
	#g_dll_ht_tool=##return;
	if(#g_dll_ht_tool==0){
		return false;
	}
	return true;

FreeDll:
	if(#g_dll_ht_tool!=0){
		freedll #g_dll_ht_tool;
		#g_dll_ht_tool=0;
	}
	return true;


//#region debug
//==========================================================================
//	debug
//==========================================================================
DumpActionProperty:
	debuginfo 1;
	debuginfo "==== action property ====";
	debuginfo "name          ="+$g_ini_property_name			;
	debuginfo "description   ="+$g_ini_property_description		;
	debuginfo "default_action="+$g_ini_property_default_action	;
	debuginfo "inheritance   ="+$g_ini_property_inheritance		;
	return ;
