/* アクションから候補を取得する

引数	ohtorii_tools_dll_handle, root_macro_directory
返値	成功	"1"
		失敗	"0"
*/


//==========================================================================
//グローバル変数
//==========================================================================
#g_dll_ohtorii_tools=val(getarg(0));
$g_root_macro_directory=getarg(1);

//==========================================================================
//メイン処理
//==========================================================================
call main;
endmacro $$return;


main:
	/*
	選択あり	初めに選択されたアイテム
	選択無し	カーソル位置のアイテム
	*/
	
	##candidate_index=dllfuncw(#g_dll_ohtorii_tools,"RefineSearchGetFirstSelectionCandidateIndex");
	if(##candidate_index==-1){
		return "0";
	}
	$$source_name=dllfuncstrw(#g_dll_ohtorii_tools,"CandidatesGetSourceName",##candidate_index);
	$$default_kind=dllfuncstrw(#g_dll_ohtorii_tools,"SourcesGetDefaultKind",$$source_name);
	$$hidemaru_label_name=dllfuncstrw(#g_dll_ohtorii_tools,"KindsGetHidemaruLabelName",$$default_kind);
	
	$$kind_filename=$g_root_macro_directory+"\\kinds\\"+$$default_kind+".mac";
	
	debuginfo 1;
	debuginfo "$$kind_filename="+$$kind_filename;
	debuginfo "$$hidemaru_label_name="+$$hidemaru_label_name;
	
	execmacro $$kind_filename, str(#g_dll_ohtorii_tools), "do_action", $$hidemaru_label_name;
	return getresultex(-1);
