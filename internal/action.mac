/*****************************************************************************
	アクションのテーブル
*****************************************************************************/
//アクション数
#g_action_num=0;

/* アクションのメニュー名
(例)	[0]="nop";
		[1]="copy";
		[2]="open";
			:
			:
*/
$g_action_menu[0]="";

/*アクションの説明
(例)	[0]="Do nothing.";
		[1]="Copy the candidate.";
		[2]="Open file with new tab.";
			:
			:
*/
$g_action_description[0]="";

/*アクションの種類
(例)	[0]="common";
		[1]="common";
		[2]="tab";
			:
			:
*/
$g_action_kind[0]="";



/*****************************************************************************
	ユーザーが確定したアクション
*****************************************************************************/
/* 確定したアクションの種類
（例）common
*/
$g_fixed_action_kind="";

/*確定したアクション
（例）delete
*/
$g_fixed_action="";




//==========================================================================
//	アクションの生成
//==========================================================================
CreateAction:
	$$action = $$1;
	/*ソースが利用するアクションのリスト
	（書式）
	$$source_action_table[0]="common";
	$$source_action_table[1]="openable";
	##source_action_num		=2;
	*/
	$$source_action_table[0]	="";
	##source_action_num			=0;
	
	{
		$$tok 	= "\t\n";
		$$token = dllfuncstr(#g_dengaku_dll,"GETTOKEN",$$action,$$tok);
		while (1) {
			$$token = dllfuncstr(#g_dengaku_dll,"RTRIM",$$token);
			$$token = dllfuncstr(#g_dengaku_dll,"LTRIM",$$token);
			if("" != $$token){
				$$source_action_table[##source_action_num]=$$token;
				##source_action_num = ##source_action_num + 1;
			}
		    if (dllfunc(#g_dengaku_dll,"HASMORETOKENS") == 0) {
				break;
			}
		    $$token = dllfuncstr(#g_dengaku_dll,"GETTOKEN","",$$tok);
		}
	}
	
	
	{//アクションに対応するファイルが存在しているか検査を行う
		##i=0;
		while(##i<##source_action_num){
			$$kind=$$source_action_table[##i];
			$$file=$g_root_macro_directory+"\\actions\\"+$$kind+".mac";
			if(! existfile($$file)){
				message $$kind + " のファイルが見つかりません。\n\n[todo]メッセージ修正";
				return false;
			}
			##i = ##i + 1;
		}
	}
	
	
	{//アクションを解析する
		##i=0;
		while(##i<##source_action_num){
			$$kind=$$source_action_table[##i];
			$$file=$g_root_macro_directory+"\\actions\\"+$$kind+".mac";
			
			execmacro $$file,"get_action_table";
			$$action_table=getresultex(-1);
			call ParseActionTable $$action_table, $$kind;
			if(! ##return){
				return false;
			}
			/*execmacro $$action_file,"get_default_action";
			$$default_action=getresultex(-1);*/
			
			##i = ##i + 1;
		}
	}
	return true;


/* 
以下のグローバル変数を設定する。

#g_action_num
$g_action_menu
$g_action_description
$g_action_kind
*/
ParseActionTable:
	$$action_table	=$$1;
	$$kind			=$$2;
	
	/*トークン分割の結果を納めるバッファ。
	（入力）
	"nop	Do nothing.	yank	Yank the candidate."
	
	（配列の内容）
	"nop	Do nothing.	yank	Yank the candidate."
	$$buffer[0]="nop";
	$$buffer[1]="Do nothing.";
	$$buffer[2]="yank";
	$$buffer[3]="Yank the candidate.";
	##buffer_num=4;
	*/
	$$buffer[0]	="";
	##buffer_num=0;
	
	{
		$$tok 	="\t\n";
		$$token =dllfuncstr(#g_dengaku_dll,"GETTOKEN",$$action_table,$$tok);
		while (1) {
			$$token = dllfuncstr(#g_dengaku_dll,"RTRIM",$$token);
			$$token = dllfuncstr(#g_dengaku_dll,"LTRIM",$$token);
			if("" != $$token){
				$$buffer[##buffer_num]=$$token;
				##buffer_num = ##buffer_num + 1;
			}
			if (dllfunc(#g_dengaku_dll,"HASMORETOKENS") == 0) {
				break;
			}
		    $$token = dllfuncstr(#g_dengaku_dll,"GETTOKEN","",$$tok);
		}
	}
	if(##buffer_num==0){
		message $$kind+" のaction_tableが空でした。\n\n[todo]メッセージを後で修正。";
		return false;
	}
	
	if((##buffer_num%2)!=0){
		message $$kind+" のaction_tableが偶数個ではありません。\n\n[todo]メッセージを後で修正。";
		return false;
	}
	
	#g_action_num=0;
	##i=0;
	while(##i<##buffer_num){
		$g_action_menu[#g_action_num]		=$$buffer[##i];
		$g_action_description[#g_action_num]=$$buffer[##i+1];
		$g_action_kind[#g_action_num]		=$$kind;
		#g_action_num = #g_action_num + 1;
		
		##i = ##i + 2;
	}
	
	return true;


/*	アクションのメニュー処理

return	0 キャンセル
		1 確定

選択したアクションはグローバル変数に設定します。
	$g_fixed_action_kind
	$g_fixed_action
*/
ProcessActionMenu:
	menuarray $g_action_menu,#g_action_num;
	if(result==0){
		//キャンセル
		return 0;
	}
	##index=result-1;
	
	$g_fixed_action_kind=$g_action_kind[##index];
	$g_fixed_action		=$g_action_menu[##index];
	return 1;


DoActionToSelectionFiles:
	/*選択ファイルに対してアクションを実行する
	*/

	if($g_fixed_action==""){
		return false;
	}
	if(#g_fixed_filenames_num==0){
		return false;
	}
	if(true){
		$$args[0]="do_action";
		$$args[1]=$g_fixed_action;
		$$args[10]=str(#g_fixed_filenames_num);
		##args_num=11;
		##i=0;
		while(##i < #g_fixed_filenames_num){
			if(1<=##i){
				$$args[##args_num]=$$args[##args_num];
			}
			$$args[##args_num] = $$args[##args_num] + $g_fixed_filenames[##i];
			##i = ##i + 1;
			##args_num = ##args_num + 1;
		}
		$$file=$g_root_macro_directory+"\\actions\\"+$g_fixed_action_kind+".mac";
		execmacro $$file,$$args,##args_num;
		if(!result){
			return false;
		}
	}else{
		$$filelist="";
		##i=0;
		while(##i < #g_fixed_filenames_num){
			if(1<=##i){
				$$filelist=$$filelist+"\t";
			}
			$$filelist = $$filelist + $g_fixed_filenames[##i];
			##i = ##i + 1;
		}
		$$file=$g_root_macro_directory+"\\actions\\"+$g_fixed_action_kind+".mac";
		execmacro $$file,"do_action",$g_fixed_action,$$filelist;
		if(!result){
			return false;
		}
	}
	//getresultex(-1)
	return true;
