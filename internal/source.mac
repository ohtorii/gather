/* ソースから候補を取得する

引数	ohtorii_tools_dll_handle 	dllへのハンドル
		input_source_filename		ソースファイル名
		gather_arg					gather_candidates へ渡す引数

返値	成功	"1"
		失敗	"0"
*/

//==========================================================================
//	一時ファイル名
//==========================================================================
$g_candidate_filename="";


//==========================================================================
//メイン処理
//==========================================================================
#g_dll_ohtorii_tools=val(getarg(0));
call main getarg(1),getarg(2);
if(##return){
	endmacro "1";
}
endmacro "0";



//==========================================================================
//	ソースの生成処理
//==========================================================================
main:
	$$source_filename=$$1;
	$$arg=$$2;

	call LoadSourceIfNotExist $$source_filename;
	$$source_name=$$return;
	if($$source_name==""){
		return false;
	}

	/*ソースのプロパティを取得する
	*/

	/*execmacro $$source_filename, str(#g_dll_ohtorii_tools), "get_property";
	$$ini=getresultex(-1);
	if($$ini==""){
		return false;
	}
	$$source_name=dllfuncstrw(#g_dll_ohtorii_tools,"SourcesCreate",$$ini);
	if($$source_name==""){
		return false;
	}
	$$ini="";//メモリ解放*/

	/*候補を作る
	*/
	##ret		=false;
	##success	= dllfuncw(#g_dll_ohtorii_tools, "SetCurrentSourceName", $$source_name);
	if(!##success){
		return false;
	}

	$$candidate_type = dllfuncstrw(#g_dll_ohtorii_tools,"SourcesGetCandidateType",$$source_name);
	if($$candidate_type=="list"){
		call GatherCandidatesFromList $$source_filename, $$arg;
		##ret = ##return;
	}else if($$candidate_type=="filelist"){
		call GatherCandidatesFromFileList $$source_filename, $$arg;
		##ret = ##return;
	}else if($$candidate_type=="function"){
		execmacro $$source_filename, str(#g_dll_ohtorii_tools), "gather_candidates", $$arg;
		if(getresultex(-1)=="0"){
			##ret=false;
		}else{
			##ret=true;
		}
	}else{
		//未知の型名
		message sprintf("$$candidate_type が未知の型名だよ。\n"+
						"$$candidate_type=%s\n"	+
						"$$source_filename=%s\n"+
						"$$source_name=%s\n"	+
						"\n\n[todo]親切なメッセージにする", $$candidate_type,$$source_filename,$$source_name);
		##ret=false;
	}
	##success = dllfuncw(#g_dll_ohtorii_tools, "ClearCurrentSourceName");
	return ##ret;


LoadSourceIfNotExist:
	$$source_filename=$$1;
	##exist	= dllfuncw(#g_dll_ohtorii_tools, "SourcesExistFileName", $$source_filename);
	if(##exist){
		$$source_name = dllfuncstrw(#g_dll_ohtorii_tools, "SourcesFileNameToSourceName", $$source_filename);
		return $$source_name;
	}
	execmacro $$source_filename, str(#g_dll_ohtorii_tools), "get_property";
	$$ini=getresultex(-1);
	if($$ini==""){
		return "";
	}
	$$source_name = dllfuncstrw(#g_dll_ohtorii_tools,"SourcesCreate",$$ini);

	##ret=dllfuncw(#g_dll_ohtorii_tools,"SourcesAppendFileNameAndSourceName",$$source_filename,$$source_name);
	if(!##ret){
		return false;
	}
	return $$source_name;

GatherCandidatesFromList:
	$$source_filename=$$1;
	$$arg=$$2;

	execmacro $$source_filename, str(#g_dll_ohtorii_tools), "gather_candidates", $$arg;
	$$candidates=getresultex(-1);

	execmacro currentmacrodirectory+"\\get_file_basename.mac",$$source_filename;
	$$source_name=getresultex(-1);
	if($$source_name==""){
		return false;
	}

	execmacro currentmacrodirectory+"\\load_dll.mac","DengakuDLL.dll";
	##dll=val(getresultex(-1));
	if(##dll==0){
		return false;
	}

	##success=true;
	$$token = dllfuncstr(##dll,"GETTOKEN",$$candidates,"\n\r");
	while (1) {
	    if($$token!=""){
			##ret=dllfuncw(#g_dll_ohtorii_tools, "CandidatesAppend", $$source_name, $$token, "");
			if(##ret==-1){
				##success=false;
				break;
			}
		}
	    if (dllfunc(##dll,"HASMORETOKENS") == 0) {
			break;
		}
	    $$token = dllfuncstr(##dll,"GETTOKEN","","\n\r");
	}

	freedll ##dll;
	return ##success;


GatherCandidatesFromFileList:
	$$source_filename=$$1;
	$$arg=$$2;

	execmacro $$source_filename, str(#g_dll_ohtorii_tools), "gather_candidates", $$arg;
	$$filelist_name=getresultex(-1);
	if($$filelist_name==""){
		return false;
	}
	debuginfo "@1";
	//
	//$g_file_image変数へファイル内容を読み込む
	//
	call LoadFileImage $$filelist_name;
	if(! ##return){
		return false;
	}
	debuginfo "@2";

	//
	//$$candidate_list変数を候補に登録する
	//
	##num_candidate = split($$candidate_list,$g_file_image,"\r\n" );
	debuginfo "##num_candidate="+str(##num_candidate);
	$g_file_image="";
	##i=0;
	while(##i < ##num_candidate){
		##success=dllfuncw(#g_dll_ohtorii_tools, "AppendCandidate", $$candidate_list[##i],"");
		##success=dllfuncw(#g_dll_ohtorii_tools, "SetCandidateActionFileName", $$candidate_list[##i]);

		##i = ##i + 1;
	}
	debuginfo "@3";

	return true;


LoadFileImage:
	##current_hidemaru=hidemaruhandle(0);
	openfile "/h /n " + $$1;
	if(! result){
		return false;
	}
	##new_hidemaru=hidemaruhandle(0);
	selectall;
	$g_file_image=gettext2(seltopcolumn,seltoplineno,selendcolumn,selendlineno);
	setactivehidemaru ##current_hidemaru;
	closehidemaruforced ##new_hidemaru;;
	return true;
