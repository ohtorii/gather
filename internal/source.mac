/* ソースから候補を取得する

引数	ohtorii_tools_dll_handle 	dllへのハンドル
		input_source_filename		ソースファイル名
		gather_arg					gather_candidates へ渡す引数

返値	成功	"1"
		失敗	"0"
*/

//==========================================================================
//	一時ファイル名
//==========================================================================
$g_candidate_filename="";


//==========================================================================
//メイン処理
//==========================================================================
debuginfo "ENTER internal\\source.mac";
#g_dll_ohtorii_tools=val(getarg(0));
#g_dll_hm_process=val(getarg(1));

call main getarg(2),getarg(3);
if(##return){
	endmacro "1";
}
endmacro "0";



//==========================================================================
//	ソースの生成処理
//==========================================================================
main:
	debuginfo "ENTER internal\\source.mac main";
	$$source_filename=$$1;
	$$arg=$$2;
	debuginfo "  $$source_filename="+$$source_filename;
	debuginfo "  $$arg="+$$arg;

	call LoadSourceIfNotExist $$source_filename;
	$$source_name=$$return;
	if($$source_name==""){
		return false;
	}

	/*ソースのプロパティを取得する
	*/

	/*execmacro $$source_filename, str(#g_dll_ohtorii_tools), "get_property";
	$$ini=getresultex(-1);
	if($$ini==""){
		return false;
	}
	$$source_name=dllfuncstrw(#g_dll_ohtorii_tools,"SourcesCreate",$$ini);
	if($$source_name==""){
		return false;
	}
	$$ini="";//メモリ解放*/

	/*候補を作る
	*/
	##ret		=false;
	##success	= dllfuncw(#g_dll_ohtorii_tools, "SetCurrentSourceName", $$source_name);
	if(!##success){
		return false;
	}

	$$candidate_type = dllfuncstrw(#g_dll_ohtorii_tools,"SourcesGetCandidateType",$$source_name);
	if($$candidate_type=="string"){
		call GatherCandidatesFromString $$source_filename, $$arg;
		##ret = ##return;
	}else if($$candidate_type=="file"){
		call GatherCandidatesFromFile $$source_filename, $$arg;
		##ret = ##return;
	}else if($$candidate_type=="async_file"){
		call GatherCandidatesFromASyncFile $$source_filename, $$arg;
		##ret = ##return;
	}else if($$candidate_type=="function"){
		$$args[0]="gather_candidates";
		$$args[1]=$$arg;
		$$args[29]=str(#g_dll_hm_process);
		$$args[30]=str(#g_dll_ohtorii_tools);
		execmacro $$source_filename, $$args, 31;
		if(getresultex(-1)=="0"){
			##ret=false;
		}else{
			##ret=true;
		}
	}else{
		//未知の型名
		message sprintf("$$candidate_type が未知の型名だよ。\n"+
						"$$candidate_type=%s\n"	+
						"$$source_filename=%s\n"+
						"$$source_name=%s\n"	+
						"\n\n[todo]親切なメッセージにする", $$candidate_type,$$source_filename,$$source_name);
		##ret=false;
	}
	##success = dllfuncw(#g_dll_ohtorii_tools, "ClearCurrentSourceName");
	return ##ret;


LoadSourceIfNotExist:
	$$source_filename=$$1;
	##exist	= dllfuncw(#g_dll_ohtorii_tools, "SourcesExistFileName", $$source_filename);
	if(##exist){
		$$source_name = dllfuncstrw(#g_dll_ohtorii_tools, "SourcesFileNameToSourceName", $$source_filename);
		return $$source_name;
	}
	$$args[0]="get_property";
	$$args[29]=str(#g_dll_hm_process);
	$$args[30]=str(#g_dll_ohtorii_tools);
	execmacro $$source_filename, $$args, 31;
	$$ini=getresultex(-1);
	if($$ini==""){
		return "";
	}
	$$source_name = dllfuncstrw(#g_dll_ohtorii_tools,"SourcesCreate",$$ini);

	##ret=dllfuncw(#g_dll_ohtorii_tools,"SourcesAppendFileNameAndSourceName",$$source_filename,$$source_name);
	if(!##ret){
		return false;
	}
	return $$source_name;

GatherCandidatesFromString:
	$$source_filename=$$1;
	$$arg=$$2;

	$$args[0]="gather_candidates";
	$$args[1]=$$arg;
	$$args[29]=str(#g_dll_hm_process);
	$$args[30]=str(#g_dll_ohtorii_tools);
	execmacro $$source_filename, $$args, 31;
	$$candidates=getresultex(-1);

	execmacro currentmacrodirectory+"\\get_file_basename.mac",$$source_filename;
	$$source_name=getresultex(-1);
	if($$source_name==""){
		return false;
	}

	execmacro currentmacrodirectory+"\\load_dll.mac","DengakuDLL.dll";
	##dll=val(getresultex(-1));
	if(##dll==0){
		return false;
	}

	##success=true;
	$$token = dllfuncstr(##dll,"GETTOKEN",$$candidates,"\n\r");
	while (1) {
	    if($$token!=""){
			##ret=dllfuncw(#g_dll_ohtorii_tools, "CandidatesAppend", $$source_name, $$token, "");
			if(##ret==-1){
				##success=false;
				break;
			}
		}
	    if (dllfunc(##dll,"HASMORETOKENS") == 0) {
			break;
		}
	    $$token = dllfuncstr(##dll,"GETTOKEN","","\n\r");
	}

	freedll ##dll;
	return ##success;


GatherCandidatesFromFile:
	$$source_filename=$$1;
	$$arg=$$2;

	$$args[0]="gather_candidates";
	$$args[1]=$$arg;
	$$args[29]=str(#g_dll_hm_process);
	$$args[30]=str(#g_dll_ohtorii_tools);

	execmacro $$source_filename, $$args, 31;
	$$filelist_name=getresultex(-1);
	if($$filelist_name==""){
		return false;
	}
	//
	//$g_file_image変数へファイル内容を読み込む
	//
	call LoadFileImage $$filelist_name;
	if(! ##return){
		return false;
	}

	//
	//$$candidate_list変数を候補に登録する
	//
	##num_candidate = split($$candidate_list,$g_file_image,"\r\n" );
	$g_file_image="";
	##i=0;
	while(##i < ##num_candidate){
		##success=dllfuncw(#g_dll_ohtorii_tools, "AppendCandidate", $$candidate_list[##i],"");
		##success=dllfuncw(#g_dll_ohtorii_tools, "SetCandidateActionFileName", $$candidate_list[##i]);

		##i = ##i + 1;
	}

	return true;


GatherCandidatesFromASyncFile:
	$$source_filename=$$1;
	$$arg=$$2;

	$$args[0]="gather_candidates";
	$$args[1]=$$arg;
	$$args[29]=str(#g_dll_hm_process);
	$$args[30]=str(#g_dll_ohtorii_tools);

	execmacro $$source_filename, $$args, 31;
	$$filelist_name=getresultex(-1);
	if($$filelist_name==""){
		return false;
	}

	##success=dllfuncw(#g_dll_ohtorii_tools, "AppendCandidateAsASyncFile", $$filelist_name);
	return ##success;


LoadFileImage:
	/* ファイルを変数へ読み込む

	$g_file_image　へファイル内容を読み込む
	*/
	##current_hidemaru=hidemaruhandle(0);
	openfile "/h /n " + $$1;
	if(! result){
		return false;
	}
	##new_hidemaru=hidemaruhandle(0);
	selectall;
	$g_file_image=gettext2(seltopcolumn,seltoplineno,selendcolumn,selendlineno);
	setactivehidemaru ##current_hidemaru;
	closehidemaruforced ##new_hidemaru;;
	return true;
