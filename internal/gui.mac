/* ソース選択ダイアログ

# 引数
	ソースのディレクトリ
	(例) "c:\hidemaru\makuro\unity\sources"


# 返値
	ソース名1\tソース名2\t...ソース名N\nオプション1\tオプション2\t...オプションN
	(例) "file_mru\twindow\n-auto-preview\t-foo-option"

*/

//ソースのファイルが格納されているディレクトリ
$g_sources_dir=getarg(0);

#g_dengaku_dll=0;
#g_dialog_width=80;

//GUIのオプション設定を保存したファイル
$g_ini_filename=currentmacrofilename+".ini";
$g_ini_section_name="unity_dialog_setting";

//ソース名のコントロール名
//配列長は #g_source_names_num
$g_source_checkctrl_names[0]="";

//auto-previewのON/OFF
$g_auto_preview_checkctrl_name="option_auto_preview";


//ディスクから読み込んだソース名と個数
$g_source_names[0]="";
#g_source_names_num=0;



//////////////////////////////////////////////////////////////////////////////
//	メイン処理
////////////////////////////////////////////////////////////////////////////////
call main;
endmacro $$return;


main:
	call LoadDll;
	if(! ##return){
		return "";
	}
	call LoadSources;
	if(! ##return){
		return "";
	}
	call Start;
	$$ret=$$return;
	call UnloadDllAll;
	return $$ret;


Start:
	call CreateDialog;
	if(! ##return){
		return "";
	}
	call MessageLoop;
	$$ret=$$return;
	call SaveDialogSetting;
	call EndDialog;
	return $$ret;


CreateDialog:
	if( dllfunc(#g_dengaku_dll,"NEWDIALOG", "ソース選択",#g_dialog_width,"0 0 center 'ＭＳ ゴシック'") == 0){
		message "ダイアログ生成に失敗";
		return false;
	}

	//
	//ソース一覧の作成
	//
	##i=0;
	while(##i<#g_source_names_num){
		$$source_name     = $g_source_names[##i];
		$$check_ctrl_name = "ctrl_"+$$source_name;
		if(dllfunc(#g_dengaku_dll,"NEWCONTROL",	"check", $$check_ctrl_name, "&"+$$source_name) == 0){
			message "チェックコントールの作成に失敗";
			return false;
		}
		$g_source_checkctrl_names[##i]=$$check_ctrl_name;

		//コントロールの設定を反映する
		$$state=getinistrw($g_ini_filename,$g_ini_section_name,$$check_ctrl_name);
		##n=dllfunc(#g_dengaku_dll,"SETCTRLSTATE",	$$check_ctrl_name, $$state);


		##i = ##i + 1;
	}

	//
	//オプション
	//
	if(dllfunc(#g_dengaku_dll,"NEWCONTROL",	"hline", "", "") == 0){
		message "hlinkの作成に失敗";
		return false;
	}
	if(dllfunc(#g_dengaku_dll,"NEWCONTROL",	"check", $g_auto_preview_checkctrl_name, "&auto_preview") == 0){
		message "checkの作成に失敗";
		return false;
	}
	//コントロールの設定を反映する
	$$state=getinistrw($g_ini_filename,$g_ini_section_name,$g_auto_preview_checkctrl_name);
	##n=dllfunc(#g_dengaku_dll,"SETCTRLSTATE",$g_auto_preview_checkctrl_name, $$state);

	//
	//ダイアログの表示
	//
	if (!dllfunc(#g_dengaku_dll,"SHOWDIALOG",hidemaruhandle(0),1)){
		message "SHOWDIALOG に失敗しました";
		return false;
	}
	return true;

#K_CTRL 	= 0x11;
#K_ENTER	= 0x0d;
#K_LEFT 	= 0x25;
#K_RIGHT	= 0x27;
#K_UP		= 0x26;
#K_DOWN 	= 0x28;
#K_SHIFT	= 0x10;

MessageLoop:
	while(1){
		if(iskeydown(#K_UP)){
			//フォーカス前移動

		}else if(iskeydown(#K_DOWN)){
			//フォーカス次へ移動

		}
		$$notify = dllfuncstr(#g_dengaku_dll,"WAITCTRLNOTIFY",0);
		if($$notify == "0"){
			//キャンセル
			return "";
		}else if($$notify == "1"){
			//Enter キー押下
			call GetSelectSources;
			return $$return;
		}
	}
	return "";


SaveDialogSetting:
	/*ダイアログの設定をiniファイルへ保存する

	＊iniの書式（例）
		[unity-dialog-setting]
		ctrl_file_mru=1
		ctrl_file_recursive=0
		ctrl_window=1
		option_auto_preview=0
	*/

	deletefile $g_ini_filename;

	//
	//ソース
	//
	while(##i<#g_source_names_num){
		$$ctrl_name=$g_source_checkctrl_names[##i];
		$$state = dllfuncstr(#g_dengaku_dll, "GETCTRLSTATE",$$ctrl_name);
		writeinistrw $g_ini_filename, $g_ini_section_name,$$ctrl_name,$$state;
		##i = ##i + 1;
	}

	//
	//オプション
	//
	$$state = dllfuncstr(#g_dengaku_dll, "GETCTRLSTATE",$g_auto_preview_checkctrl_name);
	writeinistrw $g_ini_filename, $g_ini_section_name,$g_auto_preview_checkctrl_name,$$state;
	return ;


GetSelectSources:
	/*選択されたソースとオプションを取得する

	返値	選択されたソースとオプションを返す
		(例) "file_mru\tprocess\n-auto-preview\t-foo-option"
	*/
	$$select_sources="";
	##i=0;
	while(##i<#g_source_names_num){
		$$state = dllfuncstr(#g_dengaku_dll, "GETCTRLSTATE",$g_source_checkctrl_names[##i]);
		if($$state=="1"){
			if($$select_sources==""){
				//初めて追加する場合
				$$select_sources=$g_source_names[##i];
			}else{
				$$select_sources=$$select_sources+"\t"+$g_source_names[##i];
			}
		}
		##i = ##i + 1;
	}
	$$options="";
	$$state = dllfuncstr(#g_dengaku_dll, "GETCTRLSTATE",$g_auto_preview_checkctrl_name);
	if($$state=="1"){
		$$options="-auto-preview";
	}
	if($$options==""){
		return $$select_sources;
	}
	return $$select_sources+"\n"+$$options;


EndDialog:
	##n=dllfunc(#g_dengaku_dll,"ENDDIALOG");
	return ;


LoadSources:
	#g_source_names_num=0;

	##objFSO    = createobject("Scripting.FileSystemObject");
	##objFolder = callmethod_returnobj(##objFSO,"GetFolder",$g_sources_dir);
	##objFiles  = getpropobj(##objFolder,"Files");
	while(1){
		##objFile = getcollection(##objFiles);
		if(##objFile==0){
			break;
		}
		$$name		=getpropstr(##objFile,"Name");
		$$basename	=member( ##objFSO, "GetBaseName", $$name );
		$$ext		=tolower(member( ##objFSO, "GetExtensionName", $$name ));
		releaseobject ##objFile;

		if($$ext!="mac"){
			continue;
		}
		$g_source_names[#g_source_names_num]=$$basename;
		#g_source_names_num = #g_source_names_num + 1;
	}
	releaseobject ##objFiles;
	releaseobject ##objFolder;
	releaseobject ##objFSO;
	return ;


SaveToFile:
	$$filename =$$1;
	$$save_text=$$2;
	##objFso =createobject("Scripting.FileSystemObject");
	##objFile=member(##objFso,"OpenTextFile",$$filename,2,1);
	member ##objFile,"Write",$$save_text;
	member ##objFile,"Close";
	return ;


LoadDll:
	execmacro currentmacrodirectory+"\\load_dll.mac", "DengakuDLL.dll";
	#g_dengaku_dll=val(getresultex(-1));
	if(#g_dengaku_dll==0){
		return false;
	}
	return true;


UnloadDllAll:
	if(#g_dengaku_dll){
		freedll(#g_dengaku_dll);
		#g_dengaku_dll=0;
	}
	return;
