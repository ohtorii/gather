/*

引数
0	ohtorii_dllのハンドル
1	コールバックのファイル名
2	コールバックのラベル名
*/
#g_dll_ohtorii_tools	=val(getarg(0));
$g_callback_filename	=getarg(1);
$g_callback_labelname	=getarg(2);


$g_separator="\x0B";	//垂直タブ
$g_marker="#layer0";



call ForeachJoin;
endmacro $$return;


ForeachJoin:
	disabledraw;
	begingroupundo;

	##old = compatiblemode;
	setcompatiblemode ##old 		|
					  0x00100000	;	/*各種の文を複数選択に対応した動作にします。（V8.40以降） */

	##old_column=column;
	##old_lineno=lineno;
	call ForeachSelection;

	##new_column=column;
	##new_lineno=lineno;

	//垂直タブを空白へ変換＆選択
	moveto2 ##new_column,##new_lineno;
	beginsel;
	moveto2 ##old_column,##old_lineno;
	endsel;

	call ReplacePaint, $g_separator, " ", $g_marker;

	// 跡地を範囲選択
	selectcolormarker $g_marker;
	deletecolormarkerall $g_marker;
	setcompatiblemode ##old;

	endgroupundo;
	return "1";


ForeachSelection:
	$$label 		=$g_callback_labelname;
	$$dll_handle	=str(#g_dll_ohtorii_tools);

	$$separator 	="1";
	$$not_separator ="0";

	$$args[0]=$$label;
	$$args[30]=$$dll_handle;

	##count	=dllfuncw(#g_dll_ohtorii_tools,"GetSelectionCount");
	##i		=0;
	while(##i < ##count){
		$$text=dllfuncstrw(#g_dll_ohtorii_tools,"GetSelectionText",##i);
		if(1<=##i){
			$$args[1]=$$separator;
			$$args[2]=$g_separator;
			execmacro $g_callback_filename, $$args, 31;
		}
		$$args[1]=$$not_separator;
		$$args[2]=$$text;
		execmacro  $g_callback_filename, $$args, 31;
		##i = ##i + 1;
	}
	return ;


ReplacePaint:
	// 全置換して塗る
	// $$1 = 検索パターン
	// $$2 = 置換文字列 (固定長、改行不可)
	// $$3 = レイヤ名
	replacedown $$1, $$2, nohilight,inselect;
	while (result) {
		colormarker 0xFFFFFF, 0x663333, 11, 2, 0, $$3, lineno, column - wcslen($$2), lineno, column;
		finddown;
	}
	escapeinselect;
	return;
